# SConstruct (script do MADAGASCAR)
#
# Objetivo: Adicionar programas ao pacote de processamento 
# sísmico Madagascar.
#
# Versão 1.0
#
# Funcionamento: Basta adicionar à sua pasta de usuário do
# pacote Madagascar e rodar este script digitando 'scons'
# no terminal. Siga as seguintes instruções.
#
#	-Adicione este script a sua pasta de usuário.
#
#	-O nome dos seus programas devem comecar com 'M' maiúsculo.
#
#	-Adicione o nome de seu programa à variável progs sem o 'M'.
#
#	-Rode este script na sua pasta digitando 'scons' no terminal.
#
#	-Mova-se para o topo do diretório Madagascar e rode 'scons install'.
#
# Dúvidas, me procure no email: rodolfo_profissional@hotmail.com
#
# Site: http://www.dirackslounge.online
#
# Programador: Rodolfo A. C. Neves (Dirack) 23/12/2018

import os, sys, re, string
sys.path.append('../../framework')
import bldutil

###############{ Adicione seus programas à progs }#######################
progs = '''
2d func func2 gauss norm
'''
##########################################################################

try:  # distributed version
    Import('env root pkgdir bindir')
    env = env.Clone()
except: # local version
    env = bldutil.Debug()
    cflags = env.get('CFLAGS','')
    if isinstance (cflags, str):
        env['CFLAGS'] = cflags.replace('-O3','-g').replace('-xHOST','')
    env.Prepend(CPPDEFINES=['DEBUG'])
    root = None
    SConscript('../../api/c/SConstruct')

if sys.platform.startswith('linux'):
    env.Prepend(CPPDEFINES=['LINUX'])

src = Glob('[a-z]*.c')

sse = None
#sse = env.get('SSE')
if string.rfind(env.get('CC'),'icc') >= 0:
    env.Append(CCFLAGS=['-restrict','-wd188'])
threads = env.get('PTHREADS')
if threads:
    env.Prepend(CPPDEFINES=['PTHREADS'])

if sse:
    env.Prepend(CPPPATH=['../../include'],
                LIBPATH=['../../lib'],
                LIBS=[env.get('DYNLIB','')+'rsf'],
                CCFLAGS=[sse],CPPDEFINES=['HAVE_SSE'])
else:
    env.Prepend(CPPPATH=['../../include'],
                LIBPATH=['../../lib'],
                LIBS=[env.get('DYNLIB','')+'rsf'])

if env['PLATFORM'] == 'darwin':
    # bug in Mac's gcc handling of OpenMP
    env.Append(CPPDEFINES=['_FORTIFY_SOURCE=0'],CCFLAGS='-O1')

for source in src:
    if source.rstr() == 'einspline.c':
        einspl_o = env.StaticObject('einspline.c')
        env.Depends(einspl_o,'einspline.h')
    else:
        inc = env.RSF_Include(source,prefix='')
        obj = env.StaticObject(source)
        env.Ignore(inc,inc)
        env.Depends(obj,inc)

# Regular progams
mains = Split(progs)
for prog in mains:
    sources = ['M' + prog]
    bldutil.depends(env,sources,'M'+prog)
    prog = env.Program(prog,map(lambda x: x + '.c',sources))
    if root:
        env.Install(bindir,prog)

######################################################################
# SELF-DOCUMENTATION
######################################################################
if root:
    user = os.path.basename(os.getcwd())
    main = 'sf%s.py' % user

    docs = map(lambda prog: env.Doc(prog,'M' + prog),mains)
    env.Depends(docs,'#/framework/rsf/doc.py')

    doc = env.RSF_Docmerge(main,docs)
    env.Install(pkgdir,doc)
